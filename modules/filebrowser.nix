{ config, lib, pkgs, ... }:

let
	cfg = config.modules.filebrowser;
	inherit (config) ports;
in {
	options.modules.filebrowser = {
		enable = lib.mkEnableOption "filebrowser";
		volume = lib.mkOption {
			type = lib.types.str;
		};
	};
	
	config = lib.mkIf cfg.enable {
		modules.webServer.hosts.filebrowser = {
			subdomain = "files";
			proxyPort = ports.filebrowser;
		};
		
		services.filebrowser = {
			enable = true;
			settings = {
				port = ports.filebrowser;
				root = "${cfg.volume}/data";
				database = "${cfg.volume}/database.db";
				auth = {
					method = "proxy";
					# TODO: typo sensitive, use config.modules.webServer...
					header = "Remote-User";
				};
			};
		};
		
		# for some ridiculous reason it's not possible to set the auth method
		# using the regular config file generated by services.filebrowser.settings
		systemd.services.filebrowser = let
			filebrowser = lib.getExe config.services.filebrowser.package;
			configFile = (pkgs.formats.json {}).generate "config.json" config.services.filebrowser.settings;
			# TODO: typo sensitive, use config.modules.webServer...
			header = "Remote-User";
		in {
			serviceConfig = {
				ExecStartPre = pkgs.writeShellScript "filebrowser-set-config" ''
					${filebrowser} --config ${configFile} config init
					${filebrowser} --config ${configFile} config set --auth.method=proxy --auth.header=${header}
					# scope needs to be empty for create-user-dir to work
					${filebrowser} --config ${configFile} config set --create-user-dir --scope=
				'';
			};
		};
	};
}
